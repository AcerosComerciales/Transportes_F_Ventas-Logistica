Imports System
Imports System.Data
Imports System.Collections.Generic

Imports ACELogistica
Imports ACDLogistica
Imports ACEVentas
Imports Microsoft.Practices.Unity
Imports Microsoft.Practices.Unity.Configuration
Imports System.Configuration
Imports DAConexion
Imports ACFramework

Public Class BGenerarDocsCompra

#Region " Variables "
   Private m_dtGenerarDocsCompra As DataTable

   Private ds_generardocscompra As DataSet
   Private d_generardocscompra As DGenerarDocsCompra

   Private m_eabas_docscompra As EABAS_DocsCompra

   Private m_listABAS_OrdenesCompra As List(Of EABAS_OrdenesCompra)
#End Region

#Region " Constructores "
   Public Sub New()
      d_generardocscompra = New DGenerarDocsCompra
   End Sub
#End Region

#Region " Propiedades "
   Public Property ABAS_DocsCompra() As EABAS_DocsCompra
      Get
         Return m_eabas_docscompra
      End Get
      Set(ByVal value As EABAS_DocsCompra)
         m_eabas_docscompra = value
      End Set
   End Property

   Public Property ListABAS_OrdenesCompra() As List(Of EABAS_OrdenesCompra)
      Get
         Return m_listABAS_OrdenesCompra
      End Get
      Set(ByVal value As List(Of EABAS_OrdenesCompra))
         m_listABAS_OrdenesCompra = value
      End Set
   End Property
#End Region

#Region " Funciones para obtencion de datos "

#End Region

#Region " Metodos "

   ''' <summary>
   ''' grabar documentod e compra
   ''' </summary>
   ''' <param name="x_usuario">codigo de usuario</param>
   ''' <param name="x_act_orden">opcion para actualizar la orden de compra</param>
   ''' <param name="x_msg">mensaje que se devuelve en caso de error</param>
   ''' <returns></returns>
   ''' <remarks></remarks>
   Public Function GrabarDocCompra(ByVal x_usuario As String, ByVal x_act_orden As Boolean, ByRef x_msg As String) As Boolean
      Try
         '' Inicializar Clase Negocio 
         Dim m_babas_docscompra As New BABAS_DocsCompra() With {.ABAS_DocsCompra = m_eabas_docscompra}
         m_babas_docscompra.ABAS_DocsCompra.DOCCO_Id = m_babas_docscompra.getCorrelativo("DOCCO_Id")
         '' Iniciar transaccion
         DAEnterprise.BeginTransaction()
         If m_babas_docscompra.Guardar(x_usuario) Then
            Dim i As Integer = 1
            '' Grabar los detalles
            For Each Item As EABAS_DocsCompraDetalle In m_babas_docscompra.ABAS_DocsCompra.ListEABAS_DocsCompraDetalle
               Item.DOCCO_Codigo = m_babas_docscompra.ABAS_DocsCompra.DOCCO_Codigo
               Item.ENTID_CodigoProveedor = m_babas_docscompra.ABAS_DocsCompra.ENTID_CodigoProveedor
               Item.DOCCD_Item = i
               Item.Instanciar(ACEInstancia.Nuevo)
               Item.ENTID_CodigoProveedor = m_eabas_docscompra.ENTID_CodigoProveedor
               i += 1
               Dim m_babas_docscompradetalle As New BABAS_DocsCompraDetalle() With {.ABAS_DocsCompraDetalle = Item}
               m_babas_docscompradetalle.Guardar(x_usuario)
            Next
         Else
            DAEnterprise.RollBackTransaction()
            x_msg &= String.Format("- No se completo el grabado de la cabecera del pedido{0}", vbNewLine)
            Return False
         End If
         '' Completar Operaciones adicionales con el Pedido/Cotizacion
         If x_act_orden Then
            '' Cambiar de estado a la orden de compra que sirvio para crear este documentos
            '' y actualizar El codigo del documento en la orden de compra
            Dim m_babas_ordencompra As New BABAS_OrdenesCompra
            m_babas_ordencompra.ABAS_OrdenesCompra = New EABAS_OrdenesCompra
            m_babas_ordencompra.ABAS_OrdenesCompra.Instanciar(ACEInstancia.Modificado)
            m_babas_ordencompra.ABAS_OrdenesCompra.ORDCO_Codigo = m_babas_docscompra.ABAS_DocsCompra.ORDCO_Codigo
            m_babas_ordencompra.ABAS_OrdenesCompra.DOCCO_Codigo = m_babas_docscompra.ABAS_DocsCompra.DOCCO_Codigo
            m_babas_ordencompra.ABAS_OrdenesCompra.ORDCO_Estado = EABAS_OrdenesCompra.getEstado(EABAS_OrdenesCompra.Estado.Confirmado)
            m_babas_ordencompra.Guardar(x_usuario)
         End If
         '' Culminar la transaccion
         DAEnterprise.CommitTransaction()
         Return True
      Catch ex As Exception
         DAEnterprise.RollBackTransaction()
         Throw ex
      End Try
   End Function

   ''' <summary>
   ''' Obtener notas de credito
   ''' </summary>
   ''' <param name="x_cadena">cadena de busqueda</param>
   ''' <param name="x_campo">campo de busqueda</param>
   ''' <param name="x_todos">cargar todos los registros, incluidos los anulados</param>
   ''' <param name="x_fecini">fecha de inicio de busqueda</param>
   ''' <param name="x_fecfin">fecha de fin de busqeuda</param>
   ''' <returns></returns>
   ''' <remarks></remarks>
   Public Function BusquedaDocsGenNotas(ByVal x_cadena As String, ByVal x_campo As String, ByVal x_todos As Boolean, ByVal x_fecini As DateTime, ByVal x_fecfin As DateTime) As Boolean
      Try
         Dim _join As New List(Of ACJoin)
         _join.Add(New ACJoin(EEntidades.Esquema, EEntidades.Tabla, "Ent", ACJoin.TipoJoin.Inner _
                            , New ACCampos() {New ACCampos("ENTID_Codigo", "ENTID_CodigoProveedor")} _
                            , New ACCampos() {New ACCampos("ENTID_RazonSocial", "ENTID_Proveedor")}))
         _join.Add(New ACJoin(EAlmacenes.Esquema, EAlmacenes.Tabla, "Alma", ACJoin.TipoJoin.Inner _
                            , New ACCampos() {New ACCampos("ALMAC_Id", "ALMAC_Id")} _
                            , New ACCampos() {New ACCampos("ALMAC_Descripcion", "ALMAC_Descripcion")}))
         _join.Add(New ACJoin(ETipos.Esquema, ETipos.Tabla, "TMone", ACJoin.TipoJoin.Inner _
                            , New ACCampos() {New ACCampos("TIPOS_Codigo", "TIPOS_CodTipoMoneda")} _
                            , New ACCampos() {New ACCampos("TIPOS_Descripcion", "TIPOS_TipoMoneda")}))

         Dim _where As New Hashtable()
         If x_campo.Contains("ENTID") Then
            _where.Add(x_campo, New ACWhere(x_cadena, "Ent", "System.String", ACWhere.TipoWhere._Like))
         Else
            _where.Add(x_campo, New ACWhere(x_cadena, ACWhere.TipoWhere._Like))
         End If
         _where.Add("ORDCO_FechaDocumento", New ACWhere(x_fecini, x_fecfin, ACWhere.TipoWhere.Between))
         If Not x_todos Then
            _where.Add("ORDCO_Estado", New ACWhere(EABAS_IngresosCompra.getEstado(EABAS_IngresosCompra.Estado.Confirmado), ACWhere.TipoWhere.Igual))
         End If

         m_listABAS_OrdenesCompra = New List(Of EABAS_OrdenesCompra)
         Return d_generardocscompra.BusquedaDocsGenNota(m_listABAS_OrdenesCompra, EABAS_OrdenesCompra.Esquema, EABAS_OrdenesCompra.Tabla, _join, _where)
      Catch ex As Exception
         Throw ex
      End Try
   End Function

   ''' <summary>
   ''' Cargar el detalle del documento, pero solo la diferenca de lo entregado
   ''' </summary>
   ''' <param name="x_ordco_codigo"></param>
   ''' <returns></returns>
   ''' <remarks></remarks>
   Public Function cargarDetDifOrden(ByVal x_ordco_codigo As String) As Boolean
      Try
         m_eabas_docscompra = New EABAS_DocsCompra()
         m_eabas_docscompra.ListEABAS_DocsCompraDetalle = New List(Of EABAS_DocsCompraDetalle)()
         Return d_generardocscompra.cargarDetDifOrden(m_eabas_docscompra.ListEABAS_DocsCompraDetalle, x_ordco_codigo)
      Catch ex As Exception
         Throw ex
      End Try
   End Function

#End Region

End Class

